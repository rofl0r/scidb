<!-- ======================================================================
* Author : $Author$
* Version: $Revision: 161 $
* Date   : $Date: 2011-12-17 12:31:23 +0100 (Sat, 17 Dec 2011) $
* Url    : $URL$
======================================================================= -->

<!-- ======================================================================
* Copyright: (C) 2012-2013 Gregor Cramer
======================================================================= -->

<!-- ======================================================================
* This program is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
======================================================================= -->

TITLE Datenbankarchiv

<p>
Scidb's Datenbankarchiv stützt sich auf ein unabhängiges Archivformat, für das die
generelle Dateiendung "<abbr>.ive</abbr>" vorgesehen ist. Scidb benutzt jedoch die
spezielle Dateiendung "<abbr>.scv</abbr>". Dieses Archivformat enthält Informationen
über die enthaltenen Dateien wie Name, Größe, Komprimierungsmethode, etc.
</p>

<p>
Dieses Archivformat hat folgende Syntax (vereinfachte EBNF):
</p>

<verbatim><!-- don't translate -->
Archive     = Magic [ ArchInfo ] [ UserInfo ] { File } ;
Magic       = 'iveArch' LF ;
File        = Header ( DataDelim BinaryData | NoDataDelim );
Header      = HeadDelim LF HeaderInfo DataDelim ;
HeadDelim   = LF '&lt;-- H E A D --&gt;' ;
DataDelim   = '&lt;-- D A T A --&gt;' LF ;
NoDataDelim = '&lt;-- N O D A T A --&gt;' LF ;
HeaderInfo  =
  '&lt;FileName&gt;' { Space } FileName LF
  [ '&lt;URI&gt;' { Space } Number LF ]
  [ '&lt;FileSize&gt;' { Space } Number LF ]
  [ '&lt;Size&gt;' { Space } Number LF ]
  [ '&lt;MimeType&gt;' { Space } MimeType LF ]
  [ '&lt;Compression&gt;' { Space } Compression LF ]
  [ '&lt;Checksum&gt;' { Space } Crc32Number LF ]
  [ '&lt;Modified&gt;' { Space } Timestamp LF ]
  [ '&lt;Encoding&gt;' { Space } Encoding LF ] ;
Space       = HT | " " ;
Compression = 'raw' | 'zlib' ;
BinaryData  = { Byte } ;
ArchInfo    = '&lt;TotalSize&gt;' { Space } Number ;
UserInfo    = LF { Attribute LF } Attribute ;
Attribute   = AttrName { Space } AttrValue ;
AttrName    = "&lt;" Identifier "&gt;" ;
Identifier  = AsciiLetter { AsciiLetter } ;
AttrValue   = AsciiGraphical { AsciiGraphical } ;
Timestamp   = Date " " Time ;
Date        = Year "-" Month "-" Day ;
Time        = Hour ":" Minute ":" Second ;
</verbatim>

<p>
Diese Syntaxnotation ist etwas vereinfacht, die Reihenfolge der optionalen
Attribute innerhalb des Segments <i>HeaderInfo</i> ist tatsächlich beliebig.
</p>

<p>
<b><code>&lt;FileName&gt;</code></b> ist irgendein plattformunabhängiger, gültiger
Dateiname. Grundsätzlich ist dieser Dateiname ohne Pfadbezeichnung, ausgenommen
er beeinhaltet eine Unterverzeichnisangabe. Ein plattformunabhängiger Dateiname
darf die Zeichen "&lt;" und "&gt;" nicht enthalten. Diese Angabe darf
selbstverständlich nicht fehlen, und muß immer das erste Attribut innerhalb
<i>HeaderInfo</i> sein.
</p>

<p>
<b><code>&lt;URI&gt;</code></b> ist ein genormter Ressourcen-Identifikator, wie z.B.
<code>http://www.chess-and-more.com/any-database.scv</code>, oder
<code>file:///home/chris/Chess/MyBase.sci</code>. Es ist dem Dekodierungsprogramm
überlassen wie die vorangestellten Pfadbezeichnungen verarbeitet werden.
Es kann sich auch um eine Pfadangabe handeln, die die Position der Ursprungsdatei
enthält, inklusive dem Dateinamen. Ist kein Datensegment zu diesr Datei vorhanden,
dann handelt es sich um eine Referenz auf eine externe Datei (ausserhalb des Archivs).
</p>

<p>
<b><code>&lt;Size&gt;</code></b> enthält die Größe der Datei innerhalb des
Datensegments. Diese Angabe der internen Größe ist zugleich der Offset
zum nächsten Datei-Header (zuzüglich der Größe der Dateisegmentkennung).
Diese Angabe muß genau dann angegeben werden, wenn ein Datensegment zu dieser
Datei existiert.
</p>

<p>
<b><code>&lt;MimeType&gt;</code></b> spezifiziert den Inhalt der entpackten Datei.
Diese Angabe ist nicht zwingend, sollte aber geschrieben werden.
</p>

<p>
<b><code>&lt;Compression&gt;</code></b> spezifiziert das Kompressionsverfahren,
das auf die Daten im Datensegment angewendet wurde. Z.Zt. werden nur <i>zlib</i>
(Verwendung der zlib-Bibliothek) und <i>raw</i> (keine Kompression) erkannt.
Fehlt diese Angabe, dann wird <i>raw</i> angenommen. Dabei spielt das
Kompressionsverfahren der Ursprungsdatei keine Rolle, es handelt sich um
die Angabe des Kompressionsverfahren des Datensegments. Die Informationen
über die entpackte Datei werden mit <code>&lt;MimeType&gt;</code> spezifiziert.
</p>

<p>
<b><code>&lt;FileSize&gt;</code></b> enthält die Größe der entpackten Datei.
Diese Angabe muß vorhanden sein, wenn das Datensegment vorhanden ist.
Ist diese unbekannt, dann kann auch <code>&lt;TotalSize&gt;</code> nicht bekannt
sein.
</p>

<p>
<b><code>&lt;Checksum&gt;</code></b> ist eine mit dem CRC32-Verfahren ermittelte
Prüfsumme. Diese Angabe ist optional.
</p>

<p>
<b><code>&lt;Modified&gt;</code></b> enthält den Zeitstempel der letzten Modifikation
(GMT), dieses Datum kann vom Dekoder verwendet werden um den Modifikationszeitstempel
der extrahierten Datei wiederherzustellen.
</p>

<p>
<b><code>&lt;Encoding&gt;</code></b> spezifiziert den Zeichensatz der Datei. Diese
Angabe muß genau dann vorhanden sein, wenn die entpackte Datei eine Zeichendekodierung
(z.B. ISO-8859-1) enthält, d.h. im Falle von Binärdateien existiert keine Angabe.
</p>

<p>
<b><code>&lt;TotalSize&gt;</code></b> enthält die Summe aller (entpackten) Dateigrößen.
Diese Angabe muß genau dann vorhanden sein, wenn alle enthaltenen Dateien über eine
Angabe der entpackten Dateigröße verfügen.
</p>

<p>
<b><code>Timestamp</code></b> ist im Format <code>"YYYY-MM-DD HH:MM:SS"</code>
(Y = Jahr, M = Monat, D = Tag, H = Stunde, M = Minute, S = Sekunde).
</p>

<p>
<b><code>Magic</code></b> ist so gewählt, daß folgende Punkte gewährleistet sind:
</p>

<ul>
  <li>Lesbarkeit mit einem Texteditor.</li>
  <li>Keine Verwechslung mit einer gewöhnlichen Textdatei.</li>
</ul>

<annotation>
Das Archivformat ist insgeamt so aufgebaut, daß es mit einem geeigneten Texteditor
(der binäre Daten verkraftet) gelesen und bearbeitet werden kann.
</annotation>

<p>
<b><code>UserInfo</code></b> ist im "<abbr>.scv</abbr>"-Format folgendermaßen definiert:
</p>

<verbatim><!-- don't translate -->
UserInfo    = { LF Attributes } ;
Attributes  =
  "&lt;Format&gt;" { Space } Formats
  [ "&lt;Count&gt;" { Space } Number ] ;
Format      = 'sci'  |                   # Scidb-Datenbank
              'si3'  | 'si4'  |          # Scid-Datenbank
              'cbh'  | 'cbf'  |          # ChessBase-Datenbank
              'cdp'  |                   # Chess Assistant-Datenbank
              'dsn'  |                   # Rybka-Aquarium-Datenbank
              'cif'  | 'ccif' |          # C/CIF-Archive
              'pgn'  |                   # PGN-Archive
              'bpgn' ;                   # BPGN-Archive
</verbatim>

<p>
Ein Datenbankarchiv kann mehrere PGN-, BPGN-, und C/CIF-Archive enthalten (alle Formate).
In diesem Fall wird der Datenbankname durch die zuerst enthaltene Datei bestimmt.
</p>

<p>
PGN-, BPGN-, und CIF-Archive können auch gzip-komprimiert enthalten sein,
in diesem Fall sollte die gzip-komprimierte Datei die zusätzliche Dateiendung
.gz besitzen, z.B. "MillionBase.pgn.gz".
</p>

<p>
<b><code>&lt;Count&gt;</code></b> ist die Anzahl der enthaltenen Partien in der
Datenbank.  Wenn diese Anzahl nicht bekannt ist, z.B. bei
<acronym>PGN</acronym>-Dateien oder bei der Verwendung von <acronym>URL</acronym>'s,
dann ist dieses Attribut nicht (immer) vorhanden.
</p>

<p>
<b><code>&lt;Format&gt;</code></b> ist die Angabe des Formats der entpackten Datenbank,
dazu wird die Angabe der Dateiendung der führenden Datei verwendet (in der Regel die
Dateiendung der Indexdatei). Sind mehrere Archivformate (PGN, BPGN oder C/CIF) im
Archiv enthalten, dann werden sie kommasepariert aufgeführt.
</p>

<p>
Die Reihenfolge der Attribute <code>&lt;Count&gt;</code> und <code>&lt;Format&gt;</code>
ist beliebig.
</p>

<p>
Zum Abschluß zwei einfache Beispiele für Archive:
</p>

<verbatim><!-- don't translate -->
iveArch
&lt;TotalSize&gt; 468
&lt;Count&gt; 1
&lt;Format&gt; pgn
&lt;-- H E A D --&gt;
&lt;FileName&gt; Staunton-vs-Brodie,1851-05-27.pgn
&lt;FileSize&gt; 468
&lt;Size&gt; 468
&lt;Compression&gt; raw
&lt;Checksum&gt; 3225351655
&lt;Modified&gt; 2012-02-21 18:31:12
&lt;-- D A T A --&gt;
[Event "London knockout"]
[Site "London"]
[Date "1851.05.27"]
[Round "1.1"]
[White "Staunton, Howard"]
[Black "Brodie, Alfred"]
[Result "1-0"]
[EventDate "1851.05.27"]
[EventCountry "ENG"]
[EventType "k.o."]
[EventRounds "3"]
[ECO "C44"]
[TimeMode "normal"]
[Source "Tournaments"]
[SourceDate "2004.03.11"]

1.e4 e5 2.Nf3 Nc6 3.d4 exd4 4.Bc4 Bb4+ 5.c3 dxc3 6.O-O
Qf6 7.e5 Qe7 8.a3 cxb2 9.Bxb2 Bc5 10.Nc3 d6 11.Nd5 Qd8
12.exd6 Bxd6 13.Bxg7 Bg4 14.Re1+ Nge7 15.Nf6# 1-0
</verbatim>

<verbatim><!-- don't translate -->
iveArch
&lt;Format&gt; pgn, cif
&lt;-- H E A D --&gt;
&lt;FileName&gt; tiny.pgn
&lt;URI&gt; http://scidb.sf.net/bases/tiny-1.pgn
&lt;-- N O D A T A --&gt;
&lt;-- H E A D --&gt;
&lt;FileName&gt; tiny-2.cif
&lt;URI&gt; http://scidb.sf.net/bases/tiny-2.cif
&lt;-- N O D A T A --&gt;
</verbatim>

END

<!-- vi:set ts=2 sw=2 et filetype=html fileencoding=latin1: -->
