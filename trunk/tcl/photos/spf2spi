#!/bin/sh
#
# spf2spi:
#     Generate SPI (Scid Photo Index) file from the SPF file.
#     SPI file should be placed in the same directory as SPF file.
#
# Usage:  spf2spi filename.spf > filename.spi
# Example: spf2spi historic.spf > historic.spi
# \
exec tclsh "$0" "$@"

proc PreparePlayerName {data} {
  set data [string map {. "" " " "" - ""} [string tolower $data]]
  set index [string last ",dr" $data]
  if {$index >= 0} { set data [string range $data 0 [expr {$index - 1}]] }
  return $data
}

set src [lindex $argv 0]
set dst [lindex $argv 1]

if {$argc != 2 || [file extension "$src"] ne ".spf" || [file extension "$dst"] ne ".spi"} {
  puts stderr "ERROR! spf2spi called with wrong arguments!"
  puts stderr ""
  puts stderr " spf2spi:"
  puts stderr "     Generate SPI (Scid Photo Index) file from the SPF file."
  puts stderr "     SPI file should be placed in the same directory as SPF file."
  puts stderr ""
  puts stderr " Usage:  spf2spi filename.spf filename.spi"
  puts stderr " Example: spf2spi historic.spf historic.spi"
  exit 1
}

set fd [open "$src"]
set si [open "$dst" w]

while {[gets $fd line] >= 0} {
  if {[regexp {^photo \"(.*)\" \{$} $line -> name]} {
    set begin [tell $fd]
    while {1} {
      set end [tell $fd]
      gets $fd line
      if {[regexp {.*\}.*} $line ]} { break }
    }
	 set photoInfo($name) [list $begin [expr {$end - $begin}]]
  }
}

foreach { name } [array names photoInfo] {
  puts $si "set photo_Player([PreparePlayerName $name ]) \[list \$::load::photoFile $photoInfo($name)\]"
}

close $si
close $fd
